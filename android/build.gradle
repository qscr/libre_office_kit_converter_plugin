group = "com.example.libre_office_kit_converter_plugin"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.2.20"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.11.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

def lokTag = "v0.0.1"
def lokUrl = "https://github.com/qscr/libre_office_kit_converter_plugin/releases/download/${lokTag}/android_lok.tar.gz"

def lokWorkDir = file("$projectDir/lok")
def lokArchive = file("$lokWorkDir/android_lok.tar.gz")
def lokExtractDir = file("$lokWorkDir/extracted")

tasks.register("downloadLokAndroid") {
    outputs.file(lokArchive)

    doLast {
        lokWorkDir.mkdirs()

        if (lokArchive.exists() && lokArchive.length() > 0) {
            println("[LOK] archive already exists: $lokArchive")
            return
        }

        println("[LOK] downloading: $lokUrl")
        new URL(lokUrl).withInputStream { input ->
            lokArchive.withOutputStream { out ->
                out << input
            }
        }
        println("[LOK] downloaded: $lokArchive (${lokArchive.length()} bytes)")
    }
}

tasks.register("unpackLokAndroid") {
    dependsOn("downloadLokAndroid")
    inputs.file(lokArchive)

    doLast {
        lokExtractDir.mkdirs()

        println("[LOK] extracting tar.gz -> $lokExtractDir")

        def tarFile = file("$lokWorkDir/android_lok.tar")
        if (!tarFile.exists()) {
            ant.gunzip(src: lokArchive, dest: tarFile)
            ant.untar(src: tarFile, dest: lokExtractDir)
        }
    }
}

project.afterEvaluate {
    preBuild.dependsOn unpackLokAndroid
}

android {
    namespace = "com.example.libre_office_kit_converter_plugin"

    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main.java.srcDirs += "src/main/libreoffice"
        test.java.srcDirs += "src/test/kotlin"
        main.jniLibs.srcDirs += "$lokExtractDir.path/src/main/jni"
        main.assets.srcDirs += "$lokExtractDir.path/assets"
        main.assets.srcDirs += "$lokExtractDir.path/assets_strippedUI"
    }

    defaultConfig {
        minSdk = 24
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
